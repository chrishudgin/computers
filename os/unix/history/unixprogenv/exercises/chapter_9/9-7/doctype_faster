# doctype, revised:  synthesize proper command line for troff, faster

# Replaces invocation of echo (if not a builtin), egrep, and sort with
# a single awk command invoked by the shell (so that command line arguments
# can be passed to UNIX V7 AWK by the shell).

awk ' 
BEGIN {
	printf "cat '"$*"' | "
}
/^\.PP/	{ ms++ }
/^\.EQ/	{ eqn++ }
/^\.TS/	{ tbl++ }
/^\.PS/	{ pic++ }
/^\.IS/	{ ideal++ }
/^\.\[/	{ refer++ }
END {
	if (refer > 0) printf "refer | "
	if (pic > 0)   printf "pic | "
	if (ideal > 0) printf "ideal | "
	if (tbl > 0)   printf "tbl | "
	if (eqn > 0)   printf "eqn | "
	printf "troff "
	if (ms > 0) printf "-ms"
	printf "\n"
}
' $*
